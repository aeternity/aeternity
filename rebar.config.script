%% -*- erlang -*-

IsWindows = case os:type() of {win32, _} -> true; {_, _} -> false end,

FilterRelxRocksDb = fun(Apps) ->
                        case IsWindows of
                            false ->
                                Apps;
                            true ->
                                Apps -- [rocksdb, mnesia_rocksdb]
                        end
                    end,

FilterDepsRocksDb = fun(Deps) ->
                        case IsWindows of
                            false ->
                                Deps;
                            true ->
                                [D || {K, _} = D <- Deps, K =/= rocksdb, K =/= mnesia_rocksdb]
                        end
                    end,

% We check all overlay entries' destinations against these regular expressions,
% and replacement found matches with the corresponding .cmd entry.
OverlayReplacements = [
                       "^bin/extensions/.*"
                      ],

AdaptOverlayBin = fun(Overlay) ->
                      case IsWindows of
                          false ->
                              Overlay;
                          true ->
                              lists:foldl(
                                fun
                                    ({copy, Src0, Dest0}, Acc0) ->
                                        lists:foldl(
                                          fun(Format, Acc1) ->
                                              {ok, Re} = re:compile(Format),
                                              case re:run(Dest0, Re) of
                                                  nomatch ->
                                                      Acc1;
                                                  _ ->
                                                      Src = filename:rootname(Src0) ++ ".cmd",
                                                      Dest = filename:rootname(Dest0) ++ ".cmd",
                                                      lists:keyreplace(Dest0, 3, Acc1, {copy, Src, Dest})
                                              end
                                          end, Acc0, OverlayReplacements);
                                    (_, Acc0) ->
                                        Acc0
                                end, Overlay, Overlay)
                      end
                  end,

%% Update relx config
Relx0 = lists:keyfind(relx, 1, CONFIG),
{relx, [{release, {aeternity, _}, RelxApps0} | RelxT0]} = Relx0,
{overlay, RelxOverlay0} = lists:keyfind(overlay, 1, RelxT0),
RelxT1 = lists:keyreplace(overlay, 1, RelxT0, {overlay, AdaptOverlayBin(RelxOverlay0)}),
{ok, VersionBin} = file:read_file(<<"VERSION">>),
Version = string:trim(binary_to_list(VersionBin)),
%% the release should be in front
Relx = {relx, [{release, {aeternity, Version}, FilterRelxRocksDb(RelxApps0)}] ++ RelxT1},
CONFIG1 = lists:keyreplace(relx, 1, CONFIG, Relx),

%% Update rebar deps
{deps, Deps0} = lists:keyfind(deps, 1, CONFIG1),
lists:keyreplace(deps, 1, CONFIG1, {deps, FilterDepsRocksDb(Deps0)}).
