
contract Remote =
  function up_to       : (int)               => list(int)
  function sum         : (list(int))         => int
  function some_string : ()                  => string
  function pair        : (int, string)       => (int, string)
  function squares     : (int)               => list((int, int))
  function filter_some : (list(option(int))) => list(int)
  function all_some    : (list(option(int))) => option(list(int))

contract ComplexTypes =

  record state = { worker : Remote }

  function init(worker) = {worker = worker}

  function sum_acc(xs, n) =
    switch(xs)
      []      => n
      x :: xs => sum_acc(xs, x + n)

  // Sum a list of integers
  function sum(xs : list(int)) =
    sum_acc(xs, 0)

  function up_to_acc(n, xs) =
    switch(n)
      0 => xs
      _ => up_to_acc(n - 1, n :: xs)

  function up_to(n) = up_to_acc(n, [])

  record answer('a) = {label : string, result : 'a}

  function remote_triangle(worker, n) : answer(int) =
    let xs = worker.up_to(gas = 100000, n)
    let t  = worker.sum(xs)
    { label = "answer:", result = t }

  function remote_list(n) : list(int) =
    state.worker.up_to(n)

  function some_string() = "string"

  function remote_string() : string =
    state.worker.some_string()

  function pair(x : int, y : string) = (x, y)

  function remote_pair(n : int, s : string) : (int, string) =
    state.worker.pair(gas = 10000, n, s)

  function map(f, xs) =
    switch(xs)
      []      => []
      x :: xs => f(x) :: map(f, xs)

  function squares(n) =
    map((i) => (i, i * i), up_to(n))

  function remote_squares(n) : list((int, int)) =
    state.worker.squares(n)

  // option types

  function filter_some(xs : list(option(int))) : list(int) =
    switch(xs)
      []            => []
      None    :: ys => filter_some(ys)
      Some(x) :: ys => x :: filter_some(ys)

  function remote_filter_some(xs : list(option(int))) : list(int) =
    state.worker.filter_some(xs)

  function all_some(xs : list(option(int))) : option(list(int)) =
    switch(xs)
      []         => Some([])
      None :: ys => None
      Some(x) :: ys =>
        switch(all_some(ys))
          Some(xs) => Some(x :: xs)
          None     => None

  function remote_all_some(xs : list(option(int))) : option(list(int)) =
    state.worker.all_some(gas = 10000, xs)

